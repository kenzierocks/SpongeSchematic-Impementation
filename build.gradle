buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "http://techshroom.github.io/downloads/maven"
        }
    }
    
    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }
    dependencies {
        classpath group: 'com.techshroom', name: 'Aversion', version: '1.0.1-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'aversion-util'
apply plugin: 'aversion-maven'
apply plugin: 'aversion-apt'

compileJava {
    options.incremental = true
}

util {
    javaVersion = '1.8'
}

idea.project.languageLevel = util.javaVersion

version = '0.0.1-SNAPSHOT'
group = 'me.kenzierocks'

repositories {
    mavenCentral()
    maven {
        name "snapshots"
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    mavenLocal()
}
configurations.all {
   resolutionStrategy {
     // fail eagerly on version conflict (includes transitive dependencies)
     // e.g. multiple different versions of the same dependency (group and name are equal)
     failOnVersionConflict()

     // force certain versions of dependencies (including transitive)
     force 'com.google.auto.service:auto-service:1.0-SNAPSHOT', 'com.google.guava:guava:18.0',
           'com.google.dagger:dagger:2.0', 'com.google.auto:auto-common:0.4',
           'com.google.auto.value:auto-value:1.3-SNAPSHOT',
           'org.apache.httpcomponents:httpcore:4.3.3', 'org.apache.httpcomponents:httpclient:4.3.6',
           'commons-codec:commons-codec:1.7', 'org.slf4j:slf4j-api:1.7.16',
           'com.jcabi:jcabi-aspects:0.22', 'com.jcabi:jcabi-log:0.17'

     cacheDynamicVersionsFor 10, 'minutes'
     // don't cache changing modules at all
     cacheChangingModulesFor 0, 'seconds'
   }
}
def LOGBACK_VERSION = '1.1.5'
def SLF_VERSION = '1.7.16'
dependencies {
    compile group: 'ch.qos.logback', name: 'logback-core', version: LOGBACK_VERSION
    compile group: 'ch.qos.logback', name: 'logback-classic', version: LOGBACK_VERSION
    // Because why not?
    runtime group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.5'
    compile files('libs/JNBT_1.4.jar')
    compile group: 'com.jcabi', name: 'jcabi-xml', version: '0.17.2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: SLF_VERSION
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    // N.B. required from mavenLocal() right now, please install it yourself or with ./install-hnbt.sh
    compile group: 'me.kenzierocks', name: 'hnbt', version: '0.0.1-SNAPSHOT'
    addAPTReqWComp group: 'com.google.guava', name: 'guava', version: '18.0'
    addAPT group: 'com.google.auto.value', name: 'auto-value', version: '1.3-SNAPSHOT'
    addAPTReq name: 'aopalliance'
    addAPTReq name: 'asm'
    addAPTReq name: 'auto-value'
    addAPTReq name: 'auto-common'
    addAPTReq name: 'cglib'
    addAPTReq name: 'commons-collections'
    addAPTReq name: 'commons-lang'
    addAPTReq name: 'dagger'
    addAPTReq name: 'javawriter'
    addAPTReq name: 'javax.inject'
    addAPTReq name: 'velocity'

    testCompile group: 'junit', name: 'junit', version: '4.+'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport
